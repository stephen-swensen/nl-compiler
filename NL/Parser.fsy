%{

open Microsoft.FSharp.Text.Parsing
open Swensen.NL
open Swensen.NL.Ast
open System
module EM = CompilerMessages
//let parse_error msg =
//    failwithf "%A" msg

let range (parseState:IParseState) rangeStart rangeEnd =
    let posStart,_ = parseState.InputRange rangeStart
    let _,posEnd   = parseState.InputRange rangeEnd
    PositionRange(posStart,posEnd)

%}

%token <string> BYTE
%token <string> SBYTE

%token <string> INT16
%token <string> INT32
%token <string> INT64

%token <string> UINT16
%token <string> UINT32
%token <string> UINT64

%token <string> SINGLE
%token <string> DOUBLE

%token <String> STRING
%token <Char> CHAR
%token <Boolean> BOOL

%token NULL TYPE DEFAULT

%token ANDAND OROR //XOR
%token EQEQ NOTEQ LT GT GTEQ LTEQ
%token PLUS MINUS TIMES DIV POW BANG NOT
%token <string> IDENT
%token IN EQ
%token LPAREN RPAREN
%token COMMA DOT SEMICOLON DOUBLE_SEMICOLON
%token OPEN
%token LBRACKET RBRACKET LBRACE RBRACE
%token IF ELSE
%token SET
%token WHILE BREAK CONTINUE
%token EOF
%token CHECKED UNCHECKED
%token THROW RETHROW
%token TRY CATCH FINALLY

%right SEMICOLON
%nonassoc SET

%left OROR //XOR
%left ANDAND

%left EQEQ NOTEQ LT GT GTEQ LTEQ

%left PLUS MINUS
%left TIMES DIV
%right POW

%nonassoc UMINUS
%nonassoc BANG
//%nonassoc NOT

%start start
%type <SynTopLevel> start

%type <SynExpr> expr
%type <SynExpr list> exprList

%type <SynStmt> stmt
%type <SynStmt list> stmtList

%type <TySig> tySig
%type <TySig list> tySigList

%type <((TySig option) * (String option) * SynExpr * PositionRange) list> catchList

%type <Path> path

%%

start:
    | topLevel EOF { $1 }

ifThenElse:
    //if then else expressions
    | IF expr LBRACE expr RBRACE
        { SynExpr.IfThenElse(($2, range parseState 2 2),$4,None, range parseState 1 5) }
    | IF expr LBRACE expr RBRACE ELSE LBRACE expr RBRACE
        { SynExpr.IfThenElse(($2, range parseState 2 2), $4, Some($8), range parseState 1 9) }
	| IF expr LBRACE expr RBRACE ELSE ifThenElse
        { SynExpr.IfThenElse(($2, range parseState 2 2), $4, Some($7), range parseState 1 7) }

catchList:
    | catch { [$1] }
    | catch catchList { $1::$2 }
    | { [] }

catch:
    | CATCH LBRACE expr RBRACE 
        { (None, None, $3, range parseState 1 1) }
    | CATCH LBRACKET tySig RBRACKET LBRACE expr RBRACE 
        { (Some($3), None, $6, range parseState 1 4) }
    | CATCH path LBRACE expr RBRACE 
        { (None, Some($2.Text), $4, range parseState 1 1) }
    | CATCH LBRACKET tySig RBRACKET path LBRACE expr RBRACE 
        { (Some($3), Some($5.Text), $7, range parseState 1 4) }

path:
    | IDENT { Path($1, range parseState 1 1) }
    | IDENT DOT path { ($1, range parseState 1 1) + $3 }

tySigList:
    | tySig { [$1] }
    | tySig COMMA tySigList { $1::$3 }

tySig:
    | path LBRACKET tySigList RBRACKET { TySig($1, $3, range parseState 1 4) }
    | path { TySig($1, [], range parseState 1 1) } 

exprList:
    | expr { [$1] }
    | expr COMMA exprList { $1::$3 }
    | { [] }
    
expr: 
    //-- logical binary operators --

//    | expr XOR expr                             
//        { SynExpr.LogicBinop(SynLogicBinop.Xor, ($1, range parseState 1 1), ($3, range parseState 3 3)) }
    | expr OROR expr                             
        { SynExpr.LogicBinop(SynLogicBinop.Or, ($1, range parseState 1 1), ($3, range parseState 3 3)) }
    | expr ANDAND expr                             
        { SynExpr.LogicBinop(SynLogicBinop.And, ($1, range parseState 1 1), ($3, range parseState 3 3)) }

    //-- comparison operators --

    | expr EQEQ expr                             
        { SynExpr.ComparisonBinop(SynComparisonBinop.Eq, $1, $3, range parseState 1 3) }
    | expr NOTEQ expr                             
        { SynExpr.ComparisonBinop(SynComparisonBinop.Neq, $1, $3, range parseState 1 3) }
    | expr LT expr                             
        { SynExpr.ComparisonBinop(SynComparisonBinop.Lt, $1, $3, range parseState 1 3) }
    | expr GT expr                                 
        { SynExpr.ComparisonBinop(SynComparisonBinop.Gt, $1, $3, range parseState 1 3) }
    | expr LTEQ expr                                 
        { SynExpr.ComparisonBinop(SynComparisonBinop.LtEq, $1, $3, range parseState 1 3) }
    | expr GTEQ expr                                 
        { SynExpr.ComparisonBinop(SynComparisonBinop.GtEq, $1, $3, range parseState 1 3) }

    //-- numeric binary operators --

    | expr PLUS expr                             
        { SynExpr.NumericBinop(SynNumericBinop.Plus, $1, $3, range parseState 1 3) }
    | expr MINUS expr                             
        { SynExpr.NumericBinop(SynNumericBinop.Minus, $1, $3, range parseState 1 3) }
    | expr TIMES expr                             
        { SynExpr.NumericBinop(SynNumericBinop.Times, $1, $3, range parseState 1 3) }
    | expr DIV expr                                 
        { SynExpr.NumericBinop(SynNumericBinop.Div, $1, $3, range parseState 1 3) }
    | expr POW expr                                 
        { SynExpr.Pow($1, $3, range parseState 1 3) }
            
    //-- postfix operators --
    
//    | expr BANG                                     
//        { SynExpr.Fact($1, range parseState 1 2) }
    
    //-- prefix operators --
    
    | MINUS expr %prec UMINUS                    
        { SynExpr.UMinus($2, range parseState 1 2) }
    | BANG expr                                    
        { SynExpr.LogicalNot($2, range parseState 2 2) }

    //-- literal values --

    | BYTE
        { SynExpr.Byte($1, range parseState 1 1) }
    | SBYTE                                      
        { SynExpr.SByte($1, range parseState 1 1) }

    | INT16                                      
        { SynExpr.Int16($1, range parseState 1 1) }
    | INT32                                      
        { SynExpr.Int32($1, range parseState 1 1) }
    | INT64                                      
        { SynExpr.Int64($1, range parseState 1 1) }

    | UINT16                                      
        { SynExpr.UInt16($1, range parseState 1 1) }
    | UINT32                                      
        { SynExpr.UInt32($1, range parseState 1 1) }
    | UINT64                                      
        { SynExpr.UInt64($1, range parseState 1 1) }

    | SINGLE                                 
        { SynExpr.Single($1, range parseState 1 1) }
    | DOUBLE                                 
        { SynExpr.Double($1, range parseState 1 1) }

    | STRING                                 
        { SynExpr.String($1) }
    | CHAR                                   
        { SynExpr.Char($1) }
    | BOOL                                  
        { SynExpr.Bool($1) }

    | THROW LPAREN expr RPAREN
        { SynExpr.Throw($3, range parseState 3 3) }
    | RETHROW LPAREN RPAREN
        { SynExpr.Rethrow(range parseState 1 1) }
    
    | NULL LBRACKET tySig RBRACKET              
        { SynExpr.Null($3) }
    
    | TYPE LBRACKET tySig RBRACKET            
        { SynExpr.Typeof($3) }
    | DEFAULT LBRACKET tySig RBRACKET            
        { SynExpr.Default($3) }
    | LPAREN RPAREN
        { SynExpr.Nop }

    | TRY LBRACE expr RBRACE catchList FINALLY LBRACE expr RBRACE
        { SynExpr.TryCatchFinally($3, $5, Some($8), range parseState 1 1) }
    | TRY LBRACE expr RBRACE catchList
        { SynExpr.TryCatchFinally($3, $5, None, range parseState 1 1) }
    | TRY LBRACE expr RBRACE FINALLY LBRACE expr RBRACE
        { SynExpr.TryCatchFinally($3, [], Some($7), range parseState 1 1) }
    
    //enclosed expression
    | LPAREN expr RPAREN                      
        { $2 }
    
    //bind variable
    | path EQ expr IN expr                
       { SynExpr.Let($1.Text, ($3, range parseState 3 3), $5) }

    //var expression
    | path                                     
        { SynExpr.PathGet($1)}
    //set var expression
    | path SET expr
        { SynExpr.PathSet($1, ($3, range parseState 1 3))}

    //cast on path
    | path LBRACKET tySigList RBRACKET
        { 
            if $3.Length <> 1 then //a bit of a hack here; we need to use tySigList instead of just tySig to disambiguate grammer
                parseState.RaiseError()

            SynExpr.Cast(SynExpr.PathGet($1), $3.Head, range parseState 1 4) 
        }

    //cast on expression
    | expr LBRACKET tySig RBRACKET                
        { SynExpr.Cast($1, $3, range parseState 1 4) }

    | expr DOT path 
        { SynExpr.ExprPathGet($1,$3) }

    //set var expression
    | expr DOT path SET expr
        { SynExpr.ExprPathSet($1, $3, ($5, range parseState 1 3))}

    //non-generic constructor or static call or instance call on var
    | path LPAREN exprList RPAREN                                            
        { SynExpr.PathCall($1, [], $3, range parseState 1 4) }
    //generic constructor or static call or instance call on var
    | path LBRACKET tySigList RBRACKET LPAREN exprList RPAREN            
        { SynExpr.PathCall($1, $3, $6, range parseState 1 7) }
    //generic type non-generic static call
    | path LBRACKET tySigList RBRACKET DOT IDENT LPAREN exprList RPAREN
        { SynExpr.GenericTypeStaticCall($1.Text, $3, $6, [], $8, range parseState 1 9) }
    //generic type generic static call
    | path LBRACKET tySigList RBRACKET DOT IDENT LBRACKET tySigList RBRACKET LPAREN exprList RPAREN    
        { SynExpr.GenericTypeStaticCall($1.Text, $3, $6, $8, $11, range parseState 1 12) }
    
    //non-generic method call on expression
    | expr DOT path LPAREN exprList RPAREN        
        { SynExpr.ExprPathCall($1,$3,[],$5, range parseState 1 6) }
    //generic method call on expression
    | expr DOT path LBRACKET tySigList RBRACKET LPAREN exprList RPAREN        
        { SynExpr.ExprPathCall($1,$3,$5,$8, range parseState 1 9) }
    
    //sequential expression 
    | expr SEMICOLON expr                     
        { SynExpr.Sequential($1,($3, range parseState 3 3)) }
    
    //open namespace
    | OPEN tySig IN expr                    
        { SynExpr.OpenNamespaceOrType($2, $4) }
    
    //open assembly reference (by assembly name or file name)
    | OPEN STRING IN expr                     
        { SynExpr.OpenAssembly(($2, range parseState 2 2), $4) }

	| ifThenElse { $1 }

    //-- loops --
    | WHILE expr LBRACE expr RBRACE
        { SynExpr.WhileLoop(($2, range parseState 2 2),$4) }
    | BREAK LPAREN RPAREN
        { SynExpr.Break(range parseState 1 1) }
    | CONTINUE LPAREN RPAREN
        { SynExpr.Continue(range parseState 1 1) }
    | CHECKED LBRACE expr RBRACE
        { SynExpr.Checked($3) }
    | UNCHECKED LBRACE expr RBRACE
        { SynExpr.Unchecked($3) }

stmtList:
    | stmt { [$1] }
    | stmt stmtList { $1::$2 }

stmt:
    //open namespace
    | OPEN tySig DOUBLE_SEMICOLON
        { SynStmt.OpenNamespaceOrType($2) }
    
    //open assembly reference (by assembly name or file name)
    | OPEN STRING DOUBLE_SEMICOLON
        { SynStmt.OpenAssembly($2, range parseState 2 2) }
        
    //let statement (these can not be paths, since they are compiled as fields -- unless we want to compile them with '$' seperators)
    | path EQ expr DOUBLE_SEMICOLON
        { 
            if $1.Length > 1 then
                EM.Invalid_pathifier (range parseState 1 1) ($1.Text) "."

            SynStmt.Let($1.Text, ($3, range parseState 3 3))
        }

    | expr DOUBLE_SEMICOLON
        { SynStmt.Do($1) }

topLevel:
    | stmtList
        { SynTopLevel.StmtList($1) }
    | expr
        { SynTopLevel.Expr($1) }