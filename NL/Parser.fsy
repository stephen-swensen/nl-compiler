%{

open Microsoft.FSharp.Text.Parsing
open Swensen.NL
module EM = ErrorMessage
//let parse_error msg =
//    failwithf "%A" msg

let range (parseState:IParseState) rangeStart rangeEnd =
    let posStart,_ = parseState.InputRange rangeStart
    let _,posEnd   = parseState.InputRange rangeEnd
    PositionRange(posStart,posEnd)

%}

%token <int> INT32
%token <float> DOUBLE
%token <string> STRING
%token <char> CHAR
%token <bool> BOOL
%token NULL TYPE DEFAULT
%token ANDAND OROR //XOR
%token EQEQ NOTEQ LT GT GTEQ LTEQ
%token PLUS MINUS TIMES DIV POW BANG NOT
%token <string> NAME
%token IN EQ
%token LPAREN RPAREN
%token COMMA DOT SEMICOLON
%token OPEN
%token LBRACKET RBRACKET LBRACE RBRACE
%token IF THEN ELSE
%token SET
%token WHILE BREAK CONTINUE
%token EOF

%right SEMICOLON
%nonassoc SET

%left OROR //XOR
%left ANDAND

%left EQEQ NOTEQ LT GT GTEQ LTEQ

%left PLUS MINUS
%left TIMES DIV
%right POW

%nonassoc UMINUS
%nonassoc BANG
//%nonassoc NOT

%start start
%type <Ast.SynNlFragment> start

%type <Ast.SynExpr> expr
%type <Ast.SynExpr list> exprList

%type <Ast.SynStmt> stmt
%type <Ast.SynStmt list> stmtList

%type <Ast.TySig> tySig
%type <Ast.TySig list> tySigList

%%

start:
    | nlFragment EOF { $1 }


longName:
    | NAME { $1 }
    | NAME DOT longName { $1 + "." + $3 }

tySigList:
    | tySig { [$1] }
    | tySig COMMA tySigList { $1::$3 }

tySig:
    | longName LBRACKET tySigList RBRACKET { Ast.TySig($1,$3) }
    | longName { Ast.TySig($1,[]) } 

exprList:
    | expr { [$1] }
    | expr COMMA exprList { $1::$3 }
    | { [] }
    
expr: 
    //-- logical binary operators --

//    | expr XOR expr                             
//        { Ast.SynExpr.LogicBinop(Ast.SynLogicBinop.Xor, ($1, range parseState 1 1), ($3, range parseState 3 3)) }
    | expr OROR expr                             
        { Ast.SynExpr.LogicBinop(Ast.SynLogicBinop.Or, ($1, range parseState 1 1), ($3, range parseState 3 3)) }
    | expr ANDAND expr                             
        { Ast.SynExpr.LogicBinop(Ast.SynLogicBinop.And, ($1, range parseState 1 1), ($3, range parseState 3 3)) }

    //-- comparison operators --

    | expr EQEQ expr                             
        { Ast.SynExpr.ComparisonBinop(Ast.SynComparisonBinop.Eq, $1, $3, range parseState 1 3) }
    | expr NOTEQ expr                             
        { Ast.SynExpr.ComparisonBinop(Ast.SynComparisonBinop.Neq, $1, $3, range parseState 1 3) }
    | expr LT expr                             
        { Ast.SynExpr.ComparisonBinop(Ast.SynComparisonBinop.Lt, $1, $3, range parseState 1 3) }
    | expr GT expr                                 
        { Ast.SynExpr.ComparisonBinop(Ast.SynComparisonBinop.Gt, $1, $3, range parseState 1 3) }
    | expr LTEQ expr                                 
        { Ast.SynExpr.ComparisonBinop(Ast.SynComparisonBinop.LtEq, $1, $3, range parseState 1 3) }
    | expr GTEQ expr                                 
        { Ast.SynExpr.ComparisonBinop(Ast.SynComparisonBinop.GtEq, $1, $3, range parseState 1 3) }

    //-- numeric binary operators --

    | expr PLUS expr                             
        { Ast.SynExpr.NumericBinop(Ast.SynNumericBinop.Plus, $1, $3, range parseState 1 3) }
    | expr MINUS expr                             
        { Ast.SynExpr.NumericBinop(Ast.SynNumericBinop.Minus, $1, $3, range parseState 1 3) }
    | expr TIMES expr                             
        { Ast.SynExpr.NumericBinop(Ast.SynNumericBinop.Times, $1, $3, range parseState 1 3) }
    | expr DIV expr                                 
        { Ast.SynExpr.NumericBinop(Ast.SynNumericBinop.Div, $1, $3, range parseState 1 3) }
    | expr POW expr                                 
        { Ast.SynExpr.Pow($1, $3, range parseState 1 3) }
            
    //-- postfix operators --
    
//    | expr BANG                                     
//        { Ast.SynExpr.Fact($1, range parseState 1 2) }
    
    //-- prefix operators --
    
    | MINUS expr %prec UMINUS                    
        { Ast.SynExpr.UMinus($2, range parseState 1 2) }
    | BANG expr                                    
        { Ast.SynExpr.LogicalNot($2, range parseState 2 2) }

    //-- literal values --

    | INT32                                      
        { Ast.SynExpr.Int32($1) }
    | DOUBLE                                 
        { Ast.SynExpr.Double($1) }
    | STRING                                 
        { Ast.SynExpr.String($1) }
    | CHAR                                   
        { Ast.SynExpr.Char($1) }
    | BOOL                                  
        { Ast.SynExpr.Bool($1) }
    | NULL LBRACKET tySig RBRACKET              
        { Ast.SynExpr.Null($3, range parseState 3 3) }
    | TYPE LBRACKET tySig RBRACKET            
        { Ast.SynExpr.Typeof($3, range parseState 3 3) }
    | DEFAULT LBRACKET tySig RBRACKET            
        { Ast.SynExpr.Default($3, range parseState 3 3) }
    | LPAREN RPAREN
        { Ast.SynExpr.Nop }
    
    //enclosed expression
    | LPAREN expr RPAREN                      
        { $2 }
    
    //bind variable
    | longName EQ expr IN expr                
       { Ast.SynExpr.Let($1, ($3, range parseState 3 3), $5) }
    //var expression
    | longName                                     
        { Ast.SynExpr.Var($1, range parseState 1 1)}
    //set var expression
    | longName SET expr
        { Ast.SynExpr.VarSet(($1, range parseState 1 1), $3, range parseState 1 3)}

    //cast on var
    | longName LBRACKET tySigList RBRACKET
        { 
            if $3.Length <> 1 then //a bit of a hack here; we need to use tySigList instead of just tySig to disambiguate grammer
                parseState.RaiseError()

            Ast.SynExpr.Cast(Ast.SynExpr.Var($1, range parseState 1 1), ($3.Head, range parseState 3 3), range parseState 1 4) 
        }

    //cast on expression
    | expr LBRACKET tySig RBRACKET                
        { Ast.SynExpr.Cast($1, ($3, range parseState 3 3), range parseState 1 4) }

    //non-generic constructor or static call or instance call on var
    | longName LPAREN exprList RPAREN                                            
        { Ast.SynExpr.NameCall($1, ([], range parseState 1 4), $3, range parseState 1 4) }
    //generic constructor or static call or instance call on var
    | longName LBRACKET tySigList RBRACKET LPAREN exprList RPAREN            
        { Ast.SynExpr.NameCall($1, ($3, range parseState 3 3), $6, range parseState 1 7) }
    //generic type non-generic static call
    | longName LBRACKET tySigList RBRACKET DOT NAME LPAREN exprList RPAREN
        { Ast.SynExpr.GenericTypeStaticCall($1, ($3, range parseState 3 3), $6, [], $8, range parseState 1 9) }
    //generic type generic static call
    | longName LBRACKET tySigList RBRACKET DOT NAME LBRACKET tySigList RBRACKET LPAREN exprList RPAREN    
        { Ast.SynExpr.GenericTypeStaticCall($1, ($3, range parseState 3 3), $6, $8, $11, range parseState 1 12) }
    
    //non-generic method call on expression
    | expr DOT NAME LPAREN exprList RPAREN        
        { Ast.SynExpr.ExpCall($1,$3,([], range parseState 1 6),$5, range parseState 1 6) }
    //generic method call on expression
    | expr DOT NAME LBRACKET tySigList RBRACKET LPAREN exprList RPAREN        
        { Ast.SynExpr.ExpCall($1,$3,($5, range parseState 1 9),$8, range parseState 1 9) }
    
    //sequential expression 
    | expr SEMICOLON expr                     
        { Ast.SynExpr.Sequential($1,($3, range parseState 3 3)) }
    
    //open namespace
    | OPEN longName IN expr                    
        { Ast.SynExpr.OpenNamespace(($2,range parseState 2 2), $4) }
    
    //open assembly reference (by assembly name or file name)
    | OPEN STRING IN expr                     
        { Ast.SynExpr.OpenAssembly(($2, range parseState 2 2), $4) }

    //if then else expressions
    | IF expr LBRACE expr RBRACE
        { Ast.SynExpr.IfThenElse(($2, range parseState 2 2),$4,None, range parseState 1 5) }
    | IF expr LBRACE expr RBRACE ELSE LBRACE expr RBRACE
        { Ast.SynExpr.IfThenElse(($2, range parseState 2 2), $4, Some($8), range parseState 1 9) }

    //-- loops --
    | WHILE expr LBRACE expr RBRACE
        { Ast.SynExpr.WhileLoop(($2, range parseState 2 2),$4) }
    | BREAK LPAREN RPAREN
        { Ast.SynExpr.Break(range parseState 1 1) }
    | CONTINUE LPAREN RPAREN
        { Ast.SynExpr.Continue(range parseState 1 1) }

stmtList:
    | stmt { [$1] }
    | stmt stmtList { $1::$2 }

stmt:
    //open namespace
    | OPEN longName SEMICOLON SEMICOLON
        { Ast.SynStmt.OpenNamespace($2, range parseState 2 2) }
    
    //open assembly reference (by assembly name or file name)
    | OPEN STRING SEMICOLON SEMICOLON
        { Ast.SynStmt.OpenAssembly($2, range parseState 2 2) }
        
    //let statement (these can not be longNames, since they are compiled as fields -- unless we want to compile them with '$' seperators)
    | longName EQ expr SEMICOLON SEMICOLON
        { 
            if ($1).Contains(".") then
                EM.Invalid_identifier (range parseState 1 1) ($1) "."

            Ast.SynStmt.Let($1, ($3, range parseState 3 3))
        }

    | expr SEMICOLON SEMICOLON
        { Ast.SynStmt.Do($1) }

nlFragment:
    | stmtList
        { Ast.SynNlFragment.StmtList($1) }
    | expr
        { Ast.SynNlFragment.Expr($1) }